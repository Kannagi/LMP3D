	.global vu1Triangle_CodeStart
	.global vu1Triangle_CodeEnd

; VF 0 /
; VF 01-04 Matrix Projection
; VF 05 in number faces/vertex

; VF 08-11 in vertex
; VF 12-15 out vertex Matrix (or TextureCoord for index)
; VF 16-19 out vertex float

; VF 20-23 out vertex fixed point (index)
; VF 24-27 out vertex float origin zero (index)
; VF 29 index
; VF 30 ,31 clipping

;memory
;1-5 matrix
;6

;6-133 vertex 1
;134-261 vt 1
;262-389 vertex 2
;390-517 vt2
;518-729 index1
;730-941 index2
;942-979 buffer1 GIF
;980-1015 buffer2 GIF
;1016-1019 Matrix ?
;1020-1023 light

vu1Triangle_CodeStart:
	.p2align 4
	.vu
	
.org 0x00
	nop					b PS2_VU_Func_Vectrex
	nop					nop
.org 0x10
	nop					b PS2_VU_Func_Vectrex_Loop
	nop					nop
.org 0x20
	nop					b PS2_VU_Func_Index
	nop					nop
.org 0x30
	nop					b PS2_VU_Func_Sprite
	nop					nop

;--------------------------------------------------------------------------------
PS2_VU_Func_Vectrex:
	nop					lq VF05, 0(VI00)
	nop					iaddiu VI12, VI00, 518
	nop					iaddiu VI06, VI00, 6
	nop					iaddiu VI02, VI00, 6
	nop					mtir VI04, VF05w
	nop					mtir VI14, VF05z
	

vu1Triangle_double:
	nop					lq VF30, 942(VI00)
	nop					lqi  VF08, (VI06++)	; v1
	nop					iaddiu VI05,VI00, 2048
	nop					lq VF01, 1(VI00)
	nop					lq VF02, 2(VI00)
	itof4 VF08, VF08		lq VF03, 3(VI00)
	nop					lq VF04, 4(VI00)

	nop					nop
	nop					iaddiu VI10, VI00, 37	;DB xgkick
	mulax ACC, VF01, VF08x	lqi  VF09, (VI06++)	; v2
	madday ACC, VF02, VF08y	sq VF30, 942+37(VI00)
	maddaz ACC, VF03, VF08z	nop
	maddw VF12, VF04, VF08w	iaddiu VI03, VI00, 0

	itof4 VF09, VF09		lqi  VF10, (VI06++)	; v3
	nop					mfir.w VF31,VI05
	itof0.w VF31, VF31		iaddiu VI05,VI00, 1024
	itof4 VF10, VF10		mfir.w VF30,VI05
	itof0.w VF30, VF30		iaddiu VI11, VI00, 942
	nop					iaddi VI02, VI02, -1
vertexLoop:
	;Phase1 
	
	nop					iaddiu VI01, VI00,0
	nop					mfir.w VF29,VI01
	itof0.w VF29, VF29 nop
	
	mulax ACC, VF01, VF09x	div q, VF00w, VF12w 

	madday ACC, VF02, VF09y	lqi  VF11, (VI06++)	; v4
	maddaz ACC, VF03, VF09z	nop
	maddw VF13, VF04, VF09w	nop
	nop					nop
	itof4 VF11, VF11		sqi VF19, (VI02++)	; s v4
	mulax ACC, VF01, VF10x	nop

	;Phase2
	mulq.xyzw VF16, VF12, q	div q, VF00w, VF13w 

	madday ACC, VF02, VF10y	lqi  VF08, (VI06++)	; v1
	maddaz ACC, VF03, VF10z	nop
	maddw VF14, VF04, VF10w	nop
	nop					nop
	itof4 VF08, VF08		nop
	mulax ACC, VF01, VF11x	nop
	
	;mulw.w VF16,VF16,VF29w	nop
	;Phase3
	mulq.xyzw VF17, VF13, q	div q, VF00w, VF14w 

	madday ACC, VF02, VF11y	lqi  VF09, (VI06++)	; v2
	maddaz ACC, VF03, VF11z	sqi  VF16, (VI02++)	; s v1
	maddw VF15, VF04, VF11w	nop
	nop					nop
	itof4 VF09, VF09		nop
	nop					iaddi VI04, VI04, -1

	;mulw.w VF17,VF17,VF29w	nop
	;Phase4
	mulq.xyzw VF18, VF14, q	div q, VF00w, VF15w 
	;mulw.w VF18,VF18,VF29w	nop
	
	mulax ACC, VF01, VF08x	lqi  VF10,(VI06++)	; v3
	madday ACC, VF02, VF08y	sqi  VF17, (VI02++)	; s v2
	maddaz ACC, VF03, VF08z	nop
	maddw VF12, VF04, VF08w	sqi VF18, (VI02++)	; s v3
	itof4 VF10, VF10		nop
	nop					ibne VI04, VI00, vertexLoop
	mulq.xyzw VF19, VF15, q	nop
	;mulw.w VF19,VF19,VF29w	nop

	nop					sq VF19, 0(VI02)	; s v4
	nop					nop
	nop					nop
	nop					nop
vertexLoopOut:

	nop[E]				nop
	nop					nop
	
;--------------------------------------------------------------------------------
PS2_VU_Func_Vectrex_Loop:

	nop					iaddiu VI12, VI00, 518+212
	nop					mtir VI04, VF05y
	nop					mtir VI14, VF05x

	nop					iaddiu VI02, VI00, 262
	nop					b vu1Triangle_double
	nop					iaddiu VI06, VI00, 262
	
	
	nop 					sq VF31, 0(VI00)	; s v4

	nop[E]				nop
	nop					nop

PS2_VU_Func_Index:
	nop					lqi VF29,(VI12++)
	nop					iaddiu VI13, VI00, 0x20
	
	nop					mtir VI07, VF29x
	nop					mtir VI08, VF29y
	nop					mtir VI09, VF29z

	nop					lq  VF12, 134(VI07)	; vt1
	nop					lq  VF13, 134(VI08)	; vt2
	nop					lq  VF14, 134(VI09)	; vt3
	
	
	;nop					iaddiu VI13, VI00,0
	;nop					mfir.w VF07,VI13
	;itof0.w VF07, VF07 nop
	
	

	 

;--------------------------------------------------------------------------------
IndexLoop:
	nop					iadd VI02, VI11, VI03

	itof15 VF12, VF12		lq  VF08, 6(VI07)	; v1
	itof15 VF13, VF13		lq  VF09, 6(VI08)	; v2
	itof15 VF14,VF14		lq  VF10, 6(VI09)	; v3
	nop					iaddiu VI03, VI03, 37
	
	;mulw.xy VF12,VF12,VF08w	nop
	;mulw.xy VF13,VF13,VF09w	nop
	;mulw.xy VF14,VF14,VF10w	nop

	ftoi4.xyz VF20, VF08		fcset 0
	ftoi4.xyz VF21, VF09		iaddi VI14, VI14, -1
	ftoi4.xyz VF22, VF10		lqi VF29,(VI12++)
	sub.xyz VF16,VF08,VF09	sq  VF12, 1+0(VI02)	; s vt1

	subw.xyz VF24,VF08,VF31	sq  VF13, 1+3(VI02)	; s vt2
	subw.xyz VF25,VF09,VF31	sq  VF14, 1+6(VI02)	; s vt3
	subw.xyz VF26,VF10,VF31	mtir VI07, VF29x
	sub.xyz VF17,VF10,VF09	mtir VI08, VF29y
	
	;	14 cycles
	
	clipw.xyz VF24,VF30		sq  VF20, 1+0+2(VI02)	; s v1
	clipw.xyz VF25,VF30		sq  VF21, 1+3+2(VI02)	; s v2
	clipw.xyz VF26,VF30		sq  VF22, 1+6+2(VI02)	; s v3
	opmula.xyz ACC,VF16,VF17 	mtir VI09, VF29z
	opmsub.xyz VF18,VF17,VF16 	lq  VF12, 134(VI07)	; vt1
	nop					lq  VF13, 134(VI08)	; vt2
	nop					lq  VF14, 134(VI09)	; vt3
		
	;20 cycles
	
	nop					fcand VI01,0xFFFFFF
	nop					ibeq VI01, VI00, Clipping1
	nop					iand VI03,VI03,VI10
	nop					sq  VF22, 1+0+2(VI02)	; s v1
	nop					b BackfaceCulling1
	nop					sq  VF22, 1+3+2(VI02)	; s v2
Clipping1:

	nop					fmand VI01, VI13
	nop					ibeq VI01, VI00, BackfaceCulling1
	nop					nop		
	nop					sq  VF22, 1+0+2(VI02)	; s v1
	nop					sq  VF22, 1+3+2(VI02)	; s v2
BackfaceCulling1:

	nop					ibne VI14, VI00, IndexLoop
	nop					xgkick VI02
	;28 cycles
	
	nop					nop
	nop[E]				nop
	nop					nop

;--------------------------------------------------------------------------------
PS2_VU_Func_Sprite:

	nop					lq VF01, 0(VI00)
	nop					lq VF02, 1(VI00)
	nop					lq VF03, 2(VI00)
	nop					iaddiu VI01, VI00,0xFFFF
	nop					iaddiu VI02, VI00, 6
	
	nop					mtir VI11, VF03y  ;
	nop					mtir VI12, VF03z  ;
	
	;position/flip
	nop					mtir VI07, VF02x  ;posx
	nop					mtir VI08, VF02y  ;posy
	nop					mtir VI09, VF02z  ;flagw
	nop					mtir VI10, VF02w ;flagh
	
	nop					iadd VI07, VI07,VI11  ; +offsetx
	nop					iadd VI08, VI08,VI12  ; +offsety
	;rect
	nop					mtir VI03, VF01x   
	nop					mtir VI04, VF01y
	nop					mtir VI05, VF01z
	nop					mtir VI06, VF01w
	
	
	
	nop					iaddiu VI16, VI00,0x1<<4
	nop					mtir VI15, VF03x  ;w
	
	
;-----UV
	nop					iadd VI13, VI03,VI05  ; (rectx+rectw)
	nop					iadd VI14, VI04,VI06  ; (recty+recth)
	
	nop					iadd VI11, VI03,VI09  ; (rectx+flagw)
	nop					iadd VI12, VI04,VI10  ; (recty+flagh)
	
	nop					isub VI13, VI13,VI09  ; -flagw
	nop					isub VI14, VI14,VI10  ; -flagh

	nop					mfir.x VF12x,VI11 
	nop					mfir.y VF12y,VI12
	
	nop					mfir.x VF13x,VI13
	nop					mfir.y VF13y,VI14
	
	nop					sq.xy  VF12, 1+0(VI02)	; s vt1
	nop 					sq.xy  VF13, 1+2(VI02)	; s vt2
	
	
;----------
	
	;XYZ

	nop					iadd VI11, VI07,VI05  ; (posx+rect.w)
	nop					iadd VI12, VI08,VI06  ; (posy+rect.h)

	nop					mfir.w VF21w,VI16
	nop					mfir.w VF20w,VI16
	
	nop					iadd VI11, VI11,VI15  ; +w
	
	nop					mfir.z VF20z,VI01 
	nop					mfir.x VF20x,VI07 
	nop					mfir.y VF20y,VI08

	nop					mfir.z VF21z,VI01 
	nop					mfir.x VF21x,VI11
	nop					mfir.y VF21y,VI12
	
	
	nop					sq.xyzw  VF20, 1+0+1(VI02)	; s v1
	nop					sq.xyzw  VF21, 1+2+1(VI02)	; s v2
	;------

	nop					xgkick VI02
	
	nop					nop
	nop[E]				nop
	nop					nop


vu1Triangle_CodeEnd:
